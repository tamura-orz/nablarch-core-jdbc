group = 'com.nablarch.framework'
version = '1.2.0'
description = 'JDBC依存部分(DBアクセス、UniversalDao)'


buildscript {
  repositories {
    mavenLocal()
    maven { url "${developLibUrl}" }
    jcenter()
    
    // サードパーティ製ライブラリの格納リポジトリ
    if(project.hasProperty('nablarchExtRepoUrl')) {
       maven { url "${nablarchExtRepoUrl}" }
     }

  }
  dependencies {
    classpath "com.nablarch.dev:nablarch-gradle-plugin:${nablarchGradlePluginVersion}"
    classpath "net.saliman:gradle-cobertura-plugin:${coberturaPluginVersion}"
  }
}
apply plugin: 'com.nablarch.dev.nablarch-build'
apply plugin: 'com.nablarch.dev.nablarch-maven-deploy'
apply plugin: 'com.nablarch.dev.nablarch-sonarqube'
apply plugin: 'com.nablarch.dev.nablarch-version'
apply plugin: 'cobertura'

// ビルド時のJavaバージョンを指定する
sourceCompatibility=JavaVersion.VERSION_1_6
targetCompatibility=JavaVersion.VERSION_1_6

dependencies {
  compile "com.nablarch.framework:nablarch-core-transaction:${nablarchCoreTransactionVersion}"
  compile "com.nablarch.framework:nablarch-core-applog:${nablarchCoreApplogVersion}"
  compile "com.nablarch.framework:nablarch-core-beans:${nablarchCoreBeansVersion}"

  testCompile 'org.jmockit:jmockit:1.21'
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'


  testCompile 'com.nablarch.dev:nablarch-test-support:0.0.7'
  testCompile "com.nablarch.framework:nablarch-core-repository:${nablarchCoreRepositoryVersion}"
  testCompile "com.nablarch.framework:nablarch-core-applog:${nablarchCoreApplogVersion}"

  testRuntime 'org.slf4j:slf4j-nop:1.7.21'
}

test {

  ignoreFailures = true

  cobertura {
    coverageSourceDirs = ["${projectDir}/src/main/java/"]
    coverageIgnoreTrivial = true
    coverageFormats = ['xml']
    coverageDirs = [project.sourceSets.main.output.classesDir]
  }

}

task coverage(dependsOn: ['clean', 'generateCoberturaReportByAnt'])

def coberturaReportDir = new File(project.buildDir, "cobertura-report")

task generateCoberturaReportByAnt(dependsOn: 'cobertura') {
  def dataFile = "${project.buildDir}/cobertura/cobertura.ser"

  inputs.file file(dataFile)
  outputs.dir coberturaReportDir

  doLast {
    ant {
      taskdef(resource: 'tasks.properties',
          classpath: configurations.cobertura.asPath + ':' + configurations.testRuntime.asPath)

      'cobertura-report'(
          destdir: coberturaReportDir,
          datafile: dataFile,
          srcdir: "${projectDir}/src/main/java/",
          format: 'xml'
      )
    }
  }
}

sonarRunner {
  sonarProperties {
    property "sonar.java.coveragePlugin", "cobertura"
    property "sonar.cobertura.reportPath", "${coberturaReportDir}/coverage.xml"
  }
}

tasks.sonarRunner {
  dependsOn = ['coverage']
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}


